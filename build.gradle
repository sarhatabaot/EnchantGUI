plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id "io.freefair.lombok" version "6.3.0"
    id 'java-library'
    id 'org.sonarqube' version '3.3'
}

group = 'me.tychsen'
version  = '1.6.1'

sourceCompatibility = targetCompatibility = 16

repositories {
    mavenCentral()
    maven {
        name= "aikar-acf"
        url = "https://repo.aikar.co/content/groups/aikar/" }
    maven {
        name = "spigot-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "vault-repo"
        url = "https://nexus.hc.to/content/repositories/pub_releases"
    }
    maven {
        name = "bungeecord-chat"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "codemc-repo"
        url = "https://repo.codemc.org/repository/maven-public"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
    maven {
        name = "playerpoints"
        url = 'https://repo.rosewooddev.io/repository/public/'
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7.1"
    api 'org.black_ixx:playerpoints:3.2.3'
    api "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    api 'org.bstats:bstats-bukkit:3.0.0'
    api group: 'org.jetbrains', name: 'annotations', version: '23.0.0'
}

rootProject.tasks.named("processResources") {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set("")
    relocate 'org.bstats', 'me.tychsen.enchantgui.util'
    relocate 'co.aikar.commands', 'me.tychsen.enchantgui.acf'
    relocate 'co.aikar.locales', 'me.tychsen.enchantgui.locales'
}

wrapper {
    gradleVersion = "7.4.2"
}

sonarqube {
    properties {
        property "sonar.projectKey", "sarhatabaot_EnchantGUI"
        property "sonar.organization", "sarhatabaot-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

